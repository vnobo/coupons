plugins {
    id 'org.springframework.boot' version '2.3.1.RELEASE' apply false
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
}

subprojects {

    apply plugin: "idea"
    apply plugin: "java"
    apply plugin: 'io.spring.dependency-management'

    group = 'com.alexbob'
    version = '0.0.1-dev'
    sourceCompatibility = '11'

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    idea.module {
        outputDir = file("$buildDir/out/")
        testOutputDir = file("$buildDir/test")
    }

    ext {
        set("hutoolVersion", '5.1.2')
        set("weixinVersion", "3.6.7.B")
        set("hanlpVersion", "portable-1.7.4")
        set("springCloudVersion", 'Hoxton.SR5')
        set("springCloudAlibabaVersion", '2.2.1.RELEASE')
        set("springfoxSwagger2Version", "3.0.0-SNAPSHOT")
    }

    repositories {
        maven { url 'https://maven.aliyun.com/repository/central' }
        maven { url 'https://maven.aliyun.com/repository/jcenter' }
        maven { url 'https://oss.jfrog.org/artifactory/oss-snapshot-local/' }
        mavenCentral()
    }

    dependencies {
        implementation "io.springfox:springfox-boot-starter:${springfoxSwagger2Version}"
        implementation "cn.hutool:hutool-all:${hutoolVersion}"

        implementation 'org.springframework.boot:spring-boot-starter-actuator'

        implementation 'com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-config'
        implementation 'com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-discovery'

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
        annotationProcessor 'org.projectlombok:lombok'

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
        testImplementation 'io.projectreactor:reactor-test'

    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:${springCloudAlibabaVersion}"
        }
    }

    test {
        useJUnitPlatform()
    }
}


